kill(all)$

transf(u, v, a, b, c) := (1-u-v) * a + u * b + v * c$
toGlobal(u,v) := transf(u, v, [xA, yA, zA], [xB, yB, zB], [xC, yC, zC])$

T : toGlobal(u, v)$
cross(a,b) := [a[2] * b[3] - a[3] * b[2], a[3] * b[1] - a[1] * b[3], a[1] * b[2] - a[2] * b[1]]$
norm(a) := sqrt(a[1]*a[1] + a[2]*a[2] + a[3]*a[3])$

n : cross(diff(T, u), diff(T, v));
Jacobian : 1/2 * norm(n);

/*
x(u, v) := transf(u, v, xA, xB, xC) - transf(u0, v0, xA, xB, xC)$
y(u, v) := transf(u, v, yA, yB, yC) - transf(u0, v0, yA, yB, yC)$
z(u, v) := transf(u, v, zA, zB, zC) - transf(u0, v0, zA, zB, zC)$
*/

x(u, v) := transf(u, v, xA, xB, xC);
y(u, v) := transf(u, v, yA, yB, yC);
z(u, v) := transf(u, v, zA, zB, zC);

/* density */
area: Jacobian;
rho: 3 / area;

/* moments */
Mxx : Jacobian * integrate(integrate(rho * x(u,v) * x(u,v), v, 0, 1-u), u, 0, 1);
Mxy : Jacobian * integrate(integrate(rho * x(u,v) * y(u,v), v, 0, 1-u), u, 0, 1);
Mxz : Jacobian * integrate(integrate(rho * x(u,v) * z(u,v), v, 0, 1-u), u, 0, 1);
Myy : Jacobian * integrate(integrate(rho * y(u,v) * y(u,v), v, 0, 1-u), u, 0, 1);
Myz : Jacobian * integrate(integrate(rho * y(u,v) * z(u,v), v, 0, 1-u), u, 0, 1);
Mzz : Jacobian * integrate(integrate(rho * z(u,v) * z(u,v), v, 0, 1-u), u, 0, 1);

/* Inertia tensor of triangle */
Ixx :  Myy + Mzz;
Ixy : -Mxy;
Ixz : -Mxz;
Iyy :  Mxx + Mzz;
Iyz : -Myz;
Izz :  Mxx + Myy;

I: [Ixx, Ixy, Ixz, Iyy, Iyz, Izz];

I: optimize(I);
