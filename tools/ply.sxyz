#!/usr/bin/awk -f

function usg(s) {
    msg("usage: ply.sxyz <sx> <sy> <sz> <ply.in>     >      <ply.out>")
    msg("       scale coordinates")
    exit
}

BEGIN {
    sx = ARGV[1]; shift()           # scales
    if (!floatp(sx)) { msg("ply.sxyz: sx should be a number"); usg() }

    sy = ARGV[1]; shift()
    if (!floatp(sy)) { msg("ply.sxyz: sy should be a number"); usg() }

    sz = ARGV[1]; shift()
    if (!floatp(sz)) { msg("ply.sxyz: sz should be a number"); usg() }

    f  = ARGC > 1 ? ARGV[1] : "-"
    read_header(f)
    read_vert(f)
    read_rest(f)
}

function read_header(f) {
    while (getline < f > 0) {
	print
	if ($0 == "end_header") break
    }
}

function read_vert(f,   rc) {
    while (rc = getline < f > 0) {
	if (NF != 3) break
	read_vert0()
    }
    if (rc) print
}

function read_vert0(   x, y, z) {
    x = $1; y = $2; z = $3
    print sx*x, sy*y, sz*z
}

function read_rest(f) {
    while (getline < f > 0) print
}

function shift(  i) { for (i = 2; i < ARGC; i++) ARGV[i-1] = ARGV[i]; ARGC-- }
function floatp(x)  { return length(x) > 0 && x == x + 0 }
function msg(s)     { printf "%s\n", s | "cat >&2" }

# TEST: ply.sxyz.t0
# ply.sxyz 1 2 3 test_data/sphere.ply  > sc.out.ply
#
